int = @{ ("+" | "-")? ~ ASCII_DIGIT+ }
float = @{ int ~ "." ~ ASCII_DIGIT* }
ident = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC|"_")* }
function_call = ${ ident ~ args }
	args = !{"("~expression_list~")"}
var = _{ float | int | array | string | function | void | tuple }
not = { "!" ~ ( logic_arg )}
bin_not = { "~" ~ logic_arg }
logic_arg = _{ exp_in_brackets | function_call | ident | int }
equal = { equal_arg ~ "==" ~ equal_arg }
not_equal = { equal_arg ~ "!=" ~ equal_arg }
  equal_arg = _{ math_operations | not | bin_not | exp_in_brackets | function_call | var | ident }
exp_in_brackets = _{ "(" ~ ( math_operations | math_arg | equal | not_equal | and | or | bin_and | bin_or | xor | shift ) ~ ")" }
and = { logic_arg ~ "&&" ~ logic_arg }
or = { logic_arg ~ "||" ~ logic_arg }
bin_and = { logic_arg ~ "&" ~ logic_arg }
bin_or = { logic_arg ~ "|" ~ logic_arg }
xor = { logic_arg ~ "^" ~ logic_arg }
lshift = { logic_arg ~ "<<" ~ logic_arg }
rshift = { logic_arg ~ ">>" ~ logic_arg }
shift = _{ lshift | rshift }
greater = { math_arg ~ ">" ~ math_arg }
greater_equal = { math_arg ~ ">=" ~ math_arg }
lower = { math_arg ~ "<" ~ math_arg }
lower_equal = { math_arg ~ "<=" ~ math_arg }
math_arg = _{ exp_in_brackets | float | logic_arg }
pow = { math_arg ~ "**" ~ math_arg }
multiply = { math_arg ~ "*" ~ math_arg }
divide = { math_arg ~ "/" ~ math_arg }
add = { ( math_arg | string ) ~ "+" ~ ( math_arg | string ) }
subtract = { math_arg ~ "-" ~ math_arg }
modulo = { logic_arg ~ "%" ~ logic_arg }
comparasion = _{ equal | not_equal | greater | greater_equal | lower | lower_equal }
math_operations = _{ pow | modulo | multiply | divide | add | subtract }
block = { "{" ~ line* ~ "}" }
if_else = { "if" ~ expression ~ expression ~ "else" ~ expression }
if_stm = { "if" ~ expression ~ expression }
set_if_else = { "if" ~ ident ~ ":" ~ type ~ "=" ~ expression ~ expression ~ "else" ~ expression }
set_if = { "if" ~ ident ~ ":" ~ type ~ "=" ~ expression ~ expression }
match = { "match" ~ expression ~  "{" ~ match_arm* ~ "}" }
  match_arm = _{ match_type | match_value | match_other }
  match_type = { ident ~ ":" ~ type ~ "=>" ~ expression ~ "," }
  match_value = { values ~ "=>" ~ expression ~ "," }
  match_other = { "=>" ~ expression ~ "," }
  values = { expression+ }
at = { ( add | function_call | ident | array | string ) ~ "[" ~ expression ~ "]"  }
import = { "import" ~ string }
expression = _{ substitute_types | import | match | set_if_else | set_if | at | if_else | if_stm | exp_in_brackets | comparasion | math_operations | not | bin_not | and | or | bin_and | bin_or | xor | shift | function_call | ident | var | block }
expression_list = _{ (expression ~ ("," ~ expression)*)?}
array = {"{" ~ expression_list ~ "}"}
string = ${ "\"" ~ inner ~ "\"" }
  inner = @{ char* }
  char = {
      !("\"" | "\\") ~ ANY
      | "\\" ~ ("\"" | "\\" | "b" | "f" | "n" | "r" | "t")
      | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
  }
void = {"()"}
set = { return_variable ~ "=" ~ expression }
destruct_tuple = { destruct_idents ~ "=" ~ expression }
  destruct_idents =  {"(" ~ (ident ~ ("," ~ ident)*)? ~ ")"}
line = { COMMENT* ~ ( ( ( set | destruct_tuple | expression ) ~  line_end) | block ) }
	line_end = { ";" }
return_variable = @{ident}
function = {generics? ~ params~"{"~line*~"}"}
	param = {ident ~ ":" ~ type}
	params_list = _{ (param ~ ("," ~ param)*)?}
    params = {"(" ~ params_list? ~ ")"}
  generics = {"<" ~ generic_definition ~ (";" ~ generic_definition)* ~ ">"}
  generic_definition = {generic_type ~ ":" ~ type ~ ("," ~ type)*}
substitute_types = { ident ~ generics }
tuple =  {"(" ~ (expression ~ ("," ~ expression)*)? ~ ")"}
type = _{ multi | any | standard_types }
  int_type = {"int"}
  float_type = {"float"}
  string_type = {"string"}
  void_type = {"()"}
  function_type = {"function" ~ function_type_params? ~ ("->" ~ type)?}
    function_type_params = {"(" ~ (type ~ ("," ~ type)*)? ~ ")"}
  array_type = { "[" ~ type ~ "]" }
  tuple_type = {"(" ~ (type ~ ("," ~ type)*)? ~ ")"}
  standard_types = _{ int_type | float_type | string_type | void_type | function_type | array_type | tuple_type | generic_type }
  multi = { ( standard_types ) ~ ( "|" ~ standard_types )+ }
  any = {"any"}
  generic_type = { ASCII_ALPHA }
input = _{ line* ~ EOI}
block_comment = _{"/*" ~ (!"*/" ~ ANY)* ~ "*/"}
line_comment = _{"//"~(!NEWLINE ~ ANY)*}
COMMENT = _{block_comment|line_comment}
WHITESPACE = _{ " " | "\t"| NEWLINE }