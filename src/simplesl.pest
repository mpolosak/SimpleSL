ident = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC|"_")* }
var = _{ float | int | array | array_repeat | string | function | void | tuple }
  function = {params ~ return_type_decl? ~"{"~line*~"}"}
  param = {ident ~ ":" ~ type}
  params_list = _{ (param ~ ("," ~ param)*)?}
    params = {"(" ~ params_list? ~ ")"}
  return_type_decl = {("->" ~ type | return_type)}
  tuple =  {"(" ~ (expr ~ ("," ~ expr)+) ~ ")"}
  int = @{ ASCII_DIGIT+ }
  float = @{ int ~ "." ~ ASCII_DIGIT* }
  void = {"()"}
  array = {"[" ~ expression_list ~ "]"}
  array_repeat = {"[" ~ expr ~ ";" ~ expr ~ "]"}
  string = ${ "\"" ~ inner ~ "\"" }
    inner = @{ char* }
    char = {
        !("\"" | "\\") ~ ANY
        | "\\" ~ ("\"" | "\\" | "b" | "f" | "n" | "r" | "t")
        | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
    }
primary = _{ ident | var | expr_in_brackets }
  expr_in_brackets = _{ "(" ~ expr ~ ")" }
atom = _{ prefix_op? ~ primary ~ postfix_op? }
expr = { (atom ~ (bin_op ~ atom)*) }
bin_op = _{ comp | and | or | bitwise_and | bitwise_or | xor | lshift | rshift | pow | multiply | divide | add | subtract | modulo
            | map | filter | reduce }
  comp = _{ equal | not_equal | greater | greater_equal | lower | lower_equal }
    equal = { "==" }
    not_equal = { "!=" }
    lshift = { "<<" }
    rshift = { ">>" }
    greater = { ">" }
    greater_equal = { ">=" }
    lower = { "<" }
    lower_equal = { "<=" }
  and = { "&&" }
  or = { "||" }
  bitwise_and = { "&" }
  bitwise_or = { "|" }
  xor = { "^" }
  pow = { "**" }
  multiply = { "*" }
  divide = { "/" }
  add = { "+" }
  subtract = { "-" }
  modulo = { "%" }
  map = { "@" }
  filter = { "?" }
  reduce = { "$" ~ expr }
prefix_op = _{not | bitwise_not | unary_minus }
  not = { "!" }
  bitwise_not = { "~" }
  unary_minus = { "-" }
postfix_op = _{ type_filter | at | function_call }
  type_filter = { "?" ~ type }
  at = { "[" ~ expr ~ "]"  }
  function_call = {"("~expression_list~")"}
stm = _{ block | if_else | if_stm | set_if_else | if_stm
        | set_if_else | set_if | match | import | expr }
  block = { "{" ~ line* ~ "}" }
  body = _{ block | expr }
  if_else = { "if" ~ expr ~ body ~ "else" ~ stm }
  if_stm = { "if" ~ expr ~ body }
  set_if_else = { "if" ~ ident ~ ":" ~ type ~ "=" ~ expr ~ body ~ "else" ~ stm }
  set_if = { "if" ~ ident ~ ":" ~ type ~ "=" ~ expr ~ body }
  match = { "match" ~ expr ~  "{" ~ match_arm* ~ "}" }
    match_arm = _{ match_type | match_value | match_other }
    match_type = { ident ~ ":" ~ type ~ "=>" ~ body ~ "," }
    match_value = { values ~ "=>" ~ body ~ "," }
    match_other = { "=>" ~ body ~ "," }
    values = _{ (expr ~ ("," ~ expr)*)}
  import = { "import" ~ string }
expression_list = _{ (expr ~ ("," ~ expr)*)?}
line = _{ COMMENT* ~ (( function_declaration | set | destruct_tuple | stm ) ~  line_end?) }
	line_end = _{ ";" }
  function_declaration = { ident ~ "=" ~ function }
  set = { ident ~ "=" ~ stm}
  destruct_tuple = { destruct_idents ~ "=" ~ stm }
    destruct_idents =  {"(" ~ (ident ~ ("," ~ ident)*)? ~ ")"}
type = _{ multi | standard_types | any }
  int_type = {"int"}
  float_type = {"float"}
  string_type = {"string"}
  function_type = {"function" ~ function_type_params? ~ ("->" ~ return_type)?}
    function_type_params = {"(" ~ (type ~ ("," ~ type)*)? ~ ")"}
    return_type = _{ standard_types | any | "(" ~ multi ~ ")" }
  array_type = { "[" ~ type ~ "]" }
  tuple_type = {"(" ~ (type ~ ("," ~ type)+) ~ ")"}
  standard_types = _{ int_type | float_type | string_type | void | function_type | array_type | tuple_type }
  multi = { ( standard_types ) ~ ( "|" ~ standard_types )+ }
  any = {"any"}
input = _{ line* ~ EOI}
block_comment = _{"/*" ~ (!"*/" ~ ANY)* ~ "*/"}
line_comment = _{"//"~(!NEWLINE ~ ANY)*}
COMMENT = _{block_comment|line_comment}
WHITESPACE = _{ " " | "\t"| NEWLINE }